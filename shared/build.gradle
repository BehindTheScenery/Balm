plugins {
    id('java')
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'maven-publish'
}

group = mod_group
version = mod_version
archivesBaseName = "${mod_id}-common-${minecraft_version}"

repositories {
    maven {
        url = 'https://maven.minecraftforge.net'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    compileOnly 'org.spongepowered:mixin:0.8.4'

    compileOnly "mcp.mobius.waila:wthit-api:mojmap-5.5.1"

    compileOnly project(path: ":shared-bridge", configuration: "namedElements")
}

configurations {
    testImplementation.extendsFrom compileOnly
}

tasks.withType(JavaCompile) {
    source(project(":shared-bridge").sourceSets.main.allSource)
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_id}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = mod_id + "-common"
            version = project.version + (!project.version.endsWith("SNAPSHOT") ? "+" + minecraft_version : "")

            from components.java
        }
    }

    repositories {
        maven {
            var releasesRepoUrl = "https://nexus.twelveiterations.com/repository/maven-releases/"
            var snapshotsRepoUrl = "https://nexus.twelveiterations.com/repository/maven-snapshots/"
            url = uri(version.toString().endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl)
            name = "twelveIterationsNexus"
            credentials(PasswordCredentials)
        }
    }
}